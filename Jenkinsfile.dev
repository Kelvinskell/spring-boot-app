pipeline {
    agent any
    parameters {
        string(name: 'IMAGE_NAME', defaultValue: '', description: 'Enter the image repository name (e.g: kelvinskell/bh-spring-boot-app)')
    }
    environment {
        AWS_ACCESS_KEY_ID      = credentials('aws-access-key-id')
        AWS_SECRET_ACCESS_KEY  = credentials('aws-secret-access-key')
        DB_USERNAME_DEV        = credentials('db-username-dev')
        DB_PASSWORD_DEV        = credentials('db-password-dev')
        SNS_EMAIL              = credentials('sns-email-address')
        CONTAINER_NAME         = "Spring-Boot-App"
    }

    stages {
        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Run Basic Tests') {
            steps {
                script {
                    sh 'mvn test'
                }
            }
        }

        stage('Build Docker Image') {
            steps {
                script {
                    def imageTag = "${env.BUILD_ID}"  
                    def imageName = "${params.IMAGE_NAME}:${imageTag}"
                    env.IMAGE_NAME = imageName
                    
                    sh """
                    docker build -t ${imageName} .
                    """
                }
            }
        }

        stage('Scan Image with Trivy') {
            steps {
                script {
                    sh """
                    trivy image --exit-code 0 --severity HIGH,CRITICAL ${env.IMAGE_NAME}
                    """
                }
            }
        }

        stage('Push Docker Image to DockerHub') {
            steps {
                script {
                    withDockerRegistry(credentialsId: 'docker') {
                        sh """
                        docker push ${env.IMAGE_NAME}
                        """
                    }
                }
            }
        }

        stage('Deploy to Dev Environment') {
            steps {
                script {
                    dir('terraform') {
                        def environment = 'dev'
                        sh """
                        terraform init -input=false
                        terraform workspace select ${environment} || terraform workspace new ${environment}
                        terraform apply -var='db_username=${DB_USERNAME_DEV}' \
                                       -var='db_password=${DB_PASSWORD_DEV}' \
                                       -var='image_name=${env.IMAGE_NAME}' \
                                       -var='sns_email_address=${SNS_EMAIL}' \
                                       -var-file=${environment}.tfvars -auto-approve 
                        """
                        // Get values from Terraform outputs
                        env.CLUSTER_NAME_DEV = sh(script: 'terraform output -raw ecs_cluster_name', returnStdout: true).trim()
                        env.SERVICE_NAME_DEV = sh(script: 'terraform output -raw ecs_service_name', returnStdout: true).trim()
                        env.AWS_REGION_DEV = sh(script: 'terraform output -raw aws_region', returnStdout: true).trim()
                        env.SNS_TOPIC_ARN = sh(script: 'terraform output -raw sns_topic_arn', returnStdout: true).trim()
                    }
                }
            }
        }
        stage('Update ECS Service For Dev Environment') {
    steps {
        script {
            // Get the current task definition ARN for the service
            def currentTaskDefinition = sh(script: """
                aws ecs describe-services --cluster ${env.CLUSTER_NAME_DEV} \
                    --services ${env.SERVICE_NAME_DEV} \
                    --region ${env.AWS_REGION_DEV} \
                    --query 'services[0].taskDefinition' \
                    --output text
            """, returnStdout: true).trim()

            echo "Updating Dev ECS Service: Current Task Definition: ${currentTaskDefinition}"

            // Update the ECS service with the new task definition and image override
            sh """
                aws ecs update-service --cluster ${env.CLUSTER_NAME_DEV} \
                                        --service ${env.SERVICE_NAME_DEV} \
                                        --force-new-deployment \
                                        --region ${env.AWS_REGION_DEV} \
                                        --task-definition ${currentTaskDefinition}
            """
        }
    }
}
    }

    post {
        success {
            script {
                withAWS(region: "${env.AWS_REGION_DEV}") {
                def snsTopicArn = "${env.SNS_TOPIC_ARN}"
                snsPublish topicArn: snsTopicArn, message: "Deployment to Dev environment completed with image ${env.IMAGE_NAME}."
                //snsPublish topicArn: "${env.SNS_TOPIC_ARN}", message: "Deployment to Staging environment completed with image ${env.IMAGE_NAME}."
            }
            }
        }
    }
}